[toc]

# Package

> 특정 처리를 위해 관련된 PL/SQL 블럭들이 하나의 그룹을 이루는 형태
> 프로시저, 함수외 오라클에서 제공하는 다양한 PL/SQL 블럭이 포함될 수 있음
> 패키지 선언부(SPEC)와 패키지 BODY부로 구성

## Properties

1. Specification(head)
   패키지 내부에 있는 함수나 프로시저 정의(interface)
2. Subprogram(body)
   실제 프로시저나 함수에 대한 정의(implements)

## Syntax

```sql
문법) 
1) 패키지 선언부
CREATE [OR REPLACE] PACKAGE package_name 
IS|AS 
 Public type and item declarations    -- 변수, 상수, 커서 등의 선언
 Subprogram specifications            -- 서브 프로그램 선언
END package_name ; 

2) 패키지 BODY부
CREATE [OR REPLACE] PACKAGE BODY 
package_name 
IS|AS 
 Private type and item declarations   -- 변수, 상수, 커서 등의 선언
 Subprogram bodies                    -- 서브 프로그램 정의 
END package_name ; 
```

## Management

### 0. create

- PUBLIC CONST
- PUBLIC VARIABLE
- PUBLIC CURSOR
- FUNCTION(INPUT O/X)
- PROCEDURE(INPUT O/X)
- RECURSIVE FUNCTION
- NESTED FUNCTION
- COMMON EXCEPTION HANDLER

```sql
CREATE OR REPLACE PACKAGE PKG_TUTORIAL AS

  PACKAGE_NAME VARCHAR2(16) := 'PKG_TUTORIAL';
  NOW_EXECUTE_NAME VARCHAR2(64);
  LAST_EXCUTE_NAME VARCHAR2(64);

  CURSOR TIME_4_PROFILER IS
    SELECT SYSTIMESTAMP AS TS
      FROM DUAL;
      
  /* COMMON */
  FUNCTION GET_PROFILER_TIME RETURN VARCHAR2;
  PROCEDURE CALL_START(CALLER IN VARCHAR2);
  PROCEDURE CALL_SUCCESS(CALLER IN VARCHAR2);
  PROCEDURE CALL_FAILURE(CALLER IN VARCHAR2);
  /* COMMON */
  
  FUNCTION GET_TODAY RETURN CHAR;
  FUNCTION GET_YYYYMMDD(idate IN DATE) RETURN CHAR;
  FUNCTION FACTORIAL(N NUMBER) RETURN NUMBER;
  
  PROCEDURE VOID_PRINT_TODAY;
  PROCEDURE VOID_PRINT_YYYYMMDD(idate IN DATE);
END PKG_TUTORIAL;
/

CREATE OR REPLACE PACKAGE BODY PKG_TUTORIAL AS
  
  FUNCTION GET_PROFILER_TIME RETURN VARCHAR2 IS
    RESULT VARCHAR2(32);
  BEGIN
    FOR T IN TIME_4_PROFILER LOOP
      RESULT := TO_CHAR(T.TS, 'YYYY-MM-DD HH24:MI:SS FF4');
    END LOOP;
    RETURN RESULT;
  END GET_PROFILER_TIME;  
  
  PROCEDURE CALL_START (CALLER VARCHAR2) IS
  BEGIN
    NOW_EXECUTE_NAME := CALLER;
    DBMS_OUTPUT.PUT_LINE(
      CHR(10)||'============================================'||
      CHR(10)||'PACKAGE_NAME: '||PACKAGE_NAME||
      CHR(10)||'      NOW_METHOD_NAME: '||NOW_EXECUTE_NAME||
      CHR(10)||'      LAST_METHOD_NAME: '||LAST_EXCUTE_NAME||
      CHR(10)||'DO: '||GET_PROFILER_TIME()
    );
  END CALL_START;
  
  PROCEDURE CALL_SUCCESS (CALLER VARCHAR2) IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE(
      'DONE: '||GET_PROFILER_TIME()||
      CHR(10)||'============================================'
    );
    LAST_EXCUTE_NAME := CALLER;
  END CALL_SUCCESS;
  
  PROCEDURE CALL_FAILURE (CALLER VARCHAR2) IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE(
      'FAIL: '||GET_PROFILER_TIME()||
      CHR(10)||'============================================'
    );
    LAST_EXCUTE_NAME := CALLER;
  END CALL_FAILURE;  
  
  FUNCTION GET_TODAY RETURN CHAR IS
    RESULT CHAR(19);
  BEGIN
    CALL_START('GET_TODAY');
    SELECT TO_CHAR(SYSDATE, 'YYYY-MM-DD HH24:MI:SS')
      INTO RESULT
      FROM DUAL;
    CALL_SUCCESS('GET_TODAY');
    RETURN RESULT;
  EXCEPTION
    WHEN OTHERS THEN
    CALL_FAILURE('GET_TODAY');
  END GET_TODAY;
  
  FUNCTION GET_YYYYMMDD (idate DATE) RETURN CHAR IS
    RESULT CHAR(19) := TO_CHAR(idate, 'YYYY-MM-DD HH24:MI:SS');
  BEGIN
    CALL_START('GET_YYYYMMDD');
    CALL_SUCCESS('GET_YYYYMMDD');
    RETURN RESULT;
  EXCEPTION
    WHEN OTHERS THEN
    CALL_FAILURE('GET_YYYYMMDD');
  END GET_YYYYMMDD;
  
  FUNCTION FACTORIAL(n NUMBER) RETURN NUMBER IS
    ASSERTED_ERROR EXCEPTION;
  BEGIN
    CALL_START('FACTORIAL');
    IF n <= 1 THEN
      CALL_SUCCESS('FACTORIAL');
      RETURN 1;
    ELSE
      /* !!! IF YOU WANNABE FAILURE THEN UNCOMMENT
      IF N = 3 THEN
        RAISE ASSERTED_ERROR;
      END IF;
      */
      CALL_SUCCESS('FACTORIAL');
      RETURN n * FACTORIAL(n - 1); -- RECURSIVE CALL
    END IF;
  EXCEPTION
    WHEN OTHERS THEN
    CALL_FAILURE('FACTORIAL');
  END FACTORIAL;
  
  PROCEDURE VOID_PRINT_TODAY IS
  BEGIN
    CALL_START('VOID_PRINT_TODAY');
    DBMS_OUTPUT.PUT_LINE(TO_CHAR(SYSDATE, 'YYYY-MM-DD HH24:MI:SS'));
    CALL_SUCCESS('VOID_PRINT_TODAY');
  EXCEPTION
    WHEN OTHERS THEN
    CALL_FAILURE('VOID_PRINT_TODAY');
  END VOID_PRINT_TODAY;
  
  PROCEDURE VOID_PRINT_YYYYMMDD(idate DATE) IS
    RESULT CHAR(19) := TO_CHAR(idate, 'YYYY-MM-DD HH24:MI:SS');
  BEGIN
    CALL_START('VOID_PRINT_YYYYMMDD');
    DBMS_OUTPUT.PUT_LINE(RESULT);
    CALL_SUCCESS('VOID_PRINT_YYYYMMDD');
  EXCEPTION
    WHEN OTHERS THEN
    CALL_FAILURE('VOID_PRINT_YYYYMMDD');
  END VOID_PRINT_YYYYMMDD;
  
END PKG_TUTORIAL;
/

-- PACKAGE.FUNCTION
SELECT PKG_TUTORIAL.GET_TODAY(),
       PKG_TUTORIAL.GET_YYYYMMDD(SYSDATE),
       PKG_TUTORIAL.FACTORIAL(50)
  FROM DUAL;
  
-- PACKAGE.PROCEDURE
EXEC PKG_TUTORIAL.VOID_PRINT_TODAY;
EXEC PKG_TUTORIAL.VOID_PRINT_YYYYMMDD(SYSDATE);
```



### 1. describe

```sql
select *
  from dba_procedures
 where 1=1
 	 and object_type = 'PACKAGE'
   and owner = 'SCOTT'
 order by object_name, subprogram_id;
 
select *
  from dba_objects
 where 1=1
   and object_type = 'PACKAGE'
   and owner = 'SCOTT';
```



## tutorial

### 1. 입력변수 없는 패키지 생성

> package pkg_emp_sal_total
> 	void sum
> 	void avg

#### Specification

```sql
create or replace package pkg_emp_sal_total
is
	procedure sum;
  procedure avg;
end pkg_emp_sal_total;
/
```

#### **Subprogram**

```sql
create or replace package body pkg_emp_sal_total
is
procedure sum
is
	total_count     number;
  result          number;
	begin
		select count(empno), round(sum(sal))
      into total_count, result
		  from emp;
      
    dbms_output.put_line('인원수: '||total_count||', 급여총합: '||result);
      
	end sum;
procedure avg
is
	total_count     number;
  result          number;
	begin
		select count(empno), round(avg(sal))
      into total_count, result
		  from emp;
      
    dbms_output.put_line('인원수: '||total_count||', 급여평균: '||result);
      
	end avg;
end pkg_emp_sal_total;
/
```

#### Execute

```sql
exec pkg_emp_sal_total.sum;
exec pkg_emp_sal_total.avg;
```

### 2. 입력변수 있는 패키지 생성

>패키지 생성
>
>- 입력받은 학생번호에 해당하는 이름과 몸무게를 출력하는 프로시져
>- 입력받은 학생번호에 해당하는 이름과 키를 출력하는 프로시져
>
>package pkg_student
>
>- void sum
>- void avg

```sql
create or replace package pkg_student
is
	procedure print_weight (istudno in scott.student.studno%type);
  procedure print_height (istudno in scott.student.studno%type);
end pkg_student;
/

create or replace package body pkg_student
is
procedure print_weight
(istudno scott.student.studno%type)
is
  vname     scott.student.name%type;
  result    scott.student.weight%type;
	begin
    select name, weight
      into vname, result
      from scott.student
     where studno = istudno;
     
    dbms_output.put_line('이름: '||vname||', 몸무게: '||result);
	end print_weight;
procedure print_height
(istudno scott.student.studno%type)
is
  vname    scott.student.name%type;
  result  scott.student.height%type;
	begin
    select name, height
      into vname, result
      from scott.student
     where studno = istudno;
    dbms_output.put_line('이름: '||vname||', 키: '||result);
	end print_height;
end pkg_student;
/

exec pkg_student.print_weight(9411);
exec pkg_student.print_height(9411);
```

